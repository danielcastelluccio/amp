use "core"

fn print(string: &String) {
    @print_size(string.pointer, string.length)
}

fn println(string: &String) {
    print(string)
    print("\n")
}

fn error(string: String) {
    @error_size(string, length(string))
}

fn errorln(string: String) {
    error(string)
    error("\n")
}

fn read(): String {
    let string = @allocate(128)
    @read_size(string, 128)
    return String(string, 128)
}

struct File {
    id: any

    
    fn open(file: String): File {
        let file_null_terminated = file + "\0"
        let id = @open_file(file_null_terminated.pointer)
        return File(id)
    }

    fn read(file: &File): String {
        let size = @get_file_size(file.id)
        let string = @allocate(size)
        @read_file_size(file.id, string, size)
        return String(string, size)
    }

    fn write(file: &File, string: &String) {
        @write_file_size(file.id, string.pointer, string.length)
    }

    fn close(file: &File) {
        @close_file(file)
    }

    fn free_custom(file: &File) {
        file.close()
    }
}

fn execute_command(file: String, arguments: Array<String>, wait: boolean) {
	let null_terminated_file = file + "\0"
	let arguments_new = Array<any>(arguments.length + 1)
	let i = 0
	while i < arguments.length {
		let thing = arguments[i] + "\0"
		arguments_new[i] = @cast_any(thing.pointer_consume())
		i = i + 1
	}

	@execute(null_terminated_file.pointer, arguments_new.pointer, wait)

	let i = 0
	while i < arguments_new.length - 1 {
		let argument = arguments_new[i]
		@free(argument, length(argument) + 1)
		i = i + 1
	}
}

// lower bound included, upper not included
fn random_integer_range(start: integer, end: integer): integer {
    let random = @random(8)
    let value = @cast_integer(@get_8(random))
    value = value % (end - start)
    value = value + start
    @free(random, 8)
    return value
}

fn store(value: integer): any {
    let new_memory = @allocate(1)
    @set_1(new_memory, value)
    return new_memory
}

fn not_equal(value1: &any, value2: &any): boolean {
    return @not(@equal(value1, value2))  
}

access Array<A> {
    fn <A>(size: integer): Array<A> {
	    return Array<A>(@allocate(size * 8), size)
    }

    fn []=<A>(array: &Array<A>, index: integer, value: A) {
        @set_8(array.pointer + (index * 8), value)
    }

    fn []<A>(array: &Array<A>, index: integer): &A {
        return @cast_&A(@get_8(array.pointer + (index * 8)))
    }
}

access Function {
    fn call(function: &Function, parameters: Array<any>): any {
	    return @call_function(function.pointer, parameters.pointer, parameters.length)
    }
}

access String {
    fn (pointer: any, length: integer): String {
	    return String(pointer, length, false)
    }

    fn hash(string: &String): integer {
        let total = 0
        let i = 0
        while i < string.length {
            total = total + @cast_integer(@get_1(string.pointer + i))
            i = i + 1
        }
        return total
    }

    fn +(string1: &String, string2: &String): String {
        let string1_length = string1.length
        let string2_length = string2.length
        let new_memory = @allocate(string1_length + string2_length)
        @copy(string1.pointer, new_memory, string1_length)
        @copy(string2.pointer, new_memory + string1_length, string2_length)
        return String(new_memory, string1_length + string2_length)
    }

    fn clone(string: &String): String {
        let string_length = string.length
        let new_memory = @allocate(string_length)
        @copy(string.pointer, new_memory, string_length)
        return String(new_memory, string_length)
    }

    fn [](string: &String, index: integer): String {
        let new_memory = @allocate(1)
        @copy(string.pointer + index, new_memory, 1)
        return String(new_memory, 1)
    }

    fn substring(string: &String, start_index: integer, end_index: integer): String {
        let string_length = end_index - start_index
		if end_index < start_index {
	    	println("Invalid substring!")
	    	@exit()
		}
        let new_memory = @allocate(string_length)
        @copy(string.pointer + start_index, new_memory, string_length)
        return String(new_memory, string_length)
    }

    fn index(string: &String, character: String): integer {
        let i = 0
        while i < string.length {
            if string[i] == character {
                return i
            }
            i = i + 1
        }

        return 999
    }

    fn last_index(string: &String, character: String): integer {
        let index = 999
        let i = 0
        while i < string.length {
            if string[i] == character {
                index = i
            }
            i = i + 1
        }

        return index
    }

    fn split(string: &String, character: String): Array<String> {
        let list = LinkedList<String>()
        let buffer = ""
        let i = 0
        while i < string.length {
            if string[i] == character {
                list.append(buffer)
                buffer = ""
            } else {
                buffer = buffer + string[i]
            }
            i = i + 1
        }

        list.append(buffer)

        return list.to_array()
    }

    fn strip(string: &String): String {
        let actual_string = string.clone()
        while @and(@or(@or(actual_string[0] == " ", actual_string[0] == "\n"), actual_string[0] == "\t"), actual_string.length > 0) {
            actual_string = actual_string.substring(1, actual_string.length)
        }

        while @and(@or(@or(actual_string[actual_string.length - 1] == " ", actual_string[actual_string.length - 1] == "\n"), actual_string[actual_string.length - 1] == "\t"), actual_string.length > 0) {
            actual_string = actual_string.substring(0, actual_string.length - 1)
        }

        return actual_string
    }

    fn replace(string: &String, from: &String, to: &String): String {
        let built_string = ""
        let i = 0
        while i < string.length {
	    if i < string.length - from.length - 1 {
                if string.substring(i, i + from.length) == from {
                    built_string = built_string + to
		    i = i + to.length
                } else {
                    built_string = built_string + string[i]
                }
	    } else {
                built_string = built_string + string[i]
	    }
            i = i + 1
        }

        return built_string
    }
	
    fn ==(string1: &String, string2: &String): boolean {
        if string1.length != string2.length {
            return false
        }

        let i = 0
        while i < string1.length {
            if @get_1(string1.pointer + i) != @get_1(string2.pointer + i) {
                return false
            }

            i = i + 1
        }

        return true
    }
}

fn null(): any {
    return 0
}

struct LinkedList<A> {
    head: ?LinkedList_Node<A>

	fn <A>(): LinkedList<A> {
		return LinkedList<A>(LinkedList_Node<A>(null(), null()))
	}

	fn append<A>(linked_list: &LinkedList<A>, data: A) {
	    let node = linked_list.head
	    while node.next != null() {
	        node = node.next
	    }
	
	    let new_node = LinkedList_Node<A>(data, null())
	    node.next = new_node
	}

	fn append_all<A>(linked_list: &LinkedList<A>, list: LinkedList<A>) {
	    let node = linked_list.head
	    while node.next != null() {
	        node = node.next
	    }
	
        let list_node = list.head
        let i = 0
        while i < list.size() {
            list_node = list_node.next

            let temp_node = node
	        let new_node = LinkedList_Node<A>(list_node.data, null())
            node = new_node
	        temp_node.next = new_node
            list_node.data = null()
            i = i + 1
        }
	}

	fn insert<A>(linked_list: &LinkedList<A>, index: integer, data: A) {
	    let node = linked_list.head
	
	    let i = 0
	    while i < index {
	        node = node.next
	        i = i + 1
	    }
	
	    let temp_next = node.next
	    let new_node = LinkedList_Node<A>(data, @cast_any(temp_next))
	    node.next = new_node
	}

	fn remove<A>(linked_list: &LinkedList<A>, index: integer) {
	    let node = linked_list.head
	
	    let i = 0
	    while i < index {
	        node = node.next
		    i = i + 1
	    }
	
	    let temp_next_next = node.next.next
        let node_to_free = node.next
	    node.next = temp_next_next
	}

	fn size<A>(linked_list: &LinkedList<A>): integer {
	    let node = linked_list.head
	    let i = 0
	
	    while node.next != null() {
	        node = node.next
	        i = i + 1
	    }
	
	    return i
	}

    fn []<A>(linked_list: &LinkedList<A>, index: integer): &A {
        let node = linked_list.head
        let i = 0

        while i < index + 1 {
            node = node.next
            i = i + 1
        }

        return @cast_&A(node.data)
    }

    fn pop<A>(linked_list: &LinkedList<A>): A {
        if linked_list.size() == 0 {
            println("Cannot pop!")
            @exit()
        }

	    let node = linked_list.head
        let prev_node = linked_list.head
	    while node.next != null() {
            prev_node = node
	        node = node.next
	    }

        prev_node.next = null()

        let node_owned = @cast_LinkedList_Node<A>(node)
        let data = node_owned.data_consume()
        
	    return data
	}

    fn to_array<A>(linked_list: LinkedList<A>): Array<A> {
        let i = 0
        let size = linked_list.size()
        let array = Array<A>(size)
        let node = linked_list.head
        while i < size {
            node = node.next
            array[i] = @cast_A(node.data)
            node.data = null()
            i = i + 1
        }

        return array
    }

    fn clone<A>(linked_list: &LinkedList<A>): LinkedList<A> {
        let new_list = LinkedList<A>()

        let node = linked_list.head
        while node.next != 0 {
            node = node.next
            new_list.append(node.data.clone())
        }

        return new_list
    }

	fn free_custom<A>(list: &LinkedList<A>) {
		let node = list.head
		while node != 0 {
		    let new_node = node.next

            let linked_node = @cast_LinkedList_Node<A>(node)

		    node = new_node
		}
	}
}

struct LinkedList_Node<A> {
    data: ?A
    next: ?LinkedList_Node<A>

	fn free_custom<A>(node: &LinkedList_Node<A>) {
        if node.data != 0 {
            let _ = @cast_A(node.data)
        }
	}
}

struct HashMap<A, B> {
    size: integer
    array: Array<B>
    zero_check: boolean

    fn <A, B>(): HashMap<A, B> {
        return HashMap<A, B>(20)
    }

    fn <A, B>(size: integer): HashMap<A, B> {
        return HashMap<A, B>(size, Array<B>(size), true)
    }

    fn <A, B>(size: integer, zero_check: boolean): HashMap<A, B> {
        return HashMap<A, B>(size, Array<B>(size), zero_check)
    }

    fn []<A, B>(hash_map: &HashMap<A, B>, key: &A): &B {
        let position = key.hash()
        position = position % hash_map.size
    
        let value = hash_map.array[position]
        if @and(value == 0, hash_map.zero_check) {
            println("HashMap does not contain value!")
        }
        return value
    }

    fn contains<A, B>(hash_map: &HashMap<A, B>, key: &A): boolean {
        let position = key.hash()
        position = position % hash_map.size
    
        return hash_map.array[position] != null()
    }

    fn []=<A, B>(hash_map: &HashMap<A, B>, key: A, value: B) {
        let position = key.hash()
        position = position % hash_map.size

        hash_map.array[position] = value
    }
}

struct Option<A> {
    data: any

	fn empty<A>(): Option<A> {
	    return Option<A>(null())
	}
	
	fn of<A>(data: A): Option<A> {
	    return Option<A>(data)
	}
	
	fn is_value<A>(option: &Option<A>): boolean {
	    return not_equal(option.data, null())
	}
	
	fn free_custom<A>(option: &Option<A>) {
		if option.data != null() {
            let _ = @cast_A(@cast_any(option.data))
		}
	}

	fn data<A>(option: &Option<A>): &A {
		return @cast_&A(option.data)
	}
}

struct Box {
    value: any
    type: Type

    fn <A>(value: A): Box {
        return Box(value, A)
    }

    fn free_custom(box: &Box) {
        //let _ = 
    }
}

access Type {
    fn ==(type1: &Type, type2: &Type): boolean {
        return type1.name == type2.name
    }
}

access any {
    fn +(value1: &any, value2: &any): any {
        return @add(value1, value2)
    }

    fn -(value1: &any, value2: &any): any {
        return @subtract(value1, value2)
    }

    fn ==(value1: &any, value2: &any): boolean {
        return @equal(value1, value2)
    }

    fn !=(value1: &any, value2: &any): boolean {
        return not_equal(value1, value2)
    }
}

access integer {
    fn +(value1: integer, value2: integer): integer {
        return @cast_integer(@add(value1, value2))
    }

    fn <(value1: integer, value2: integer): boolean {
        return @less(value1, value2)
    }

    fn >(value1: integer, value2: integer): boolean {
        return @greater(value1, value2)
    }

    fn -(value1: integer, value2: integer): integer {
        return @cast_integer(@subtract(value1, value2))
    }

    fn %(value1: integer, value2: integer): integer {
        return @cast_integer(@modulo(value1, value2))
    }

    fn /(value1: integer, value2: integer): integer {
        return @cast_integer(@divide(value1, value2))
    }

    fn *(value1: integer, value2: integer): integer {
        return @cast_integer(@multiply(value1, value2))
    }

    fn to_string(integer: integer): String {
        let string = ""
        if integer == 0 {
            string = "0"
        }

        while integer > 0 {
            let modulo = integer % 10
            integer = integer / 10
            let prepended_string = String(store(modulo + 48), 1)
            string = prepended_string +string
        }

        return string
    }
}

access boolean {
    fn to_string(boolean: boolean): String {
        if boolean {
            return "true"
        }
        return "false"
    }
}
